apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

ext {

    daggerVersion = '2.7'
    javaxAnnotationVersion = '10.0-b28'
    rxJavaTestVersion = '1.1.0'
    rxJavaVersion = '2.0.0'
    rxAndroidVersion = '2.0.0'
    supportVersion = '25.0.1'
    gsonVersion = '2.8.0'
    retrofitVersion = '2.1.0'
    retrofitAdapterVersion = '1.0.0'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
    lombokVersion = '1.16.10'
    butterKnifeVersion = '8.4.0'
    superRecyclerViewVersion = '1.1.4'
    picassoVersion = '2.5.2'
    labelViewVersion = 'v1.1.1'
    jodaTimeAndroidVersion = '2.9.5'
    mockitoVersion = '2.2.8'
}


def getAPIProperty(String key) {
    return hasProperty(key) ? getProperty(key) : System.getenv(key)
}

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {

        applicationId 'com.carloseduardo.movie.search'
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        buildConfigField 'String', 'TMD_API_KEY', getAPIProperty('TMD_API_KEY')
    }

    buildTypes {

        release {

            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {

        dexInProcess true
        javaMaxHeapSize "2G"
    }
}

dependencies {

    //Tests
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "io.reactivex:rxjava:${rxJavaTestVersion}"

    //Compatibility
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"

    //Dependency injection
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "org.glassfish:javax.annotation:${javaxAnnotationVersion}"

    //Reactive
    compile "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    compile "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"

    //Serialization - Deserialization
    compile "com.google.code.gson:gson:${gsonVersion}"

    //Network
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:${retrofitAdapterVersion}"

    //Boilerplate Generation
    provided "org.projectlombok:lombok:${lombokVersion}"

    //Custom View
    compile "com.malinskiy:superrecyclerview:${superRecyclerViewVersion}"
    compile "com.github.linger1216:labelview:${labelViewVersion}"

    //View Binding
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    //Image Loading
    compile "com.squareup.picasso:picasso:${picassoVersion}"

    //JSR-310
    compile "net.danlew:android.joda:${jodaTimeAndroidVersion}"
}